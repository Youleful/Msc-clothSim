#version 430 core
#extension GL_ARB_compute_shader: enable
#extension GL_ARB_shader_storage_buffer_object: enable

// work group size injected before compilation
#define WORK_GROUP_SIZE_X XX
#define WORK_GROUP_SIZE_Y XX
#define EPSILON 0.0001


layout(std430, binding = 0) buffer preClothBuffer { // cloth positions in previous timestep
     vec4 preCloth[];
};
layout(std430, binding = 1) buffer curClothBuffer { // cloth positions in new timestep
     vec4 curCloth[];
};
layout(std430, binding = 2) readonly buffer bodyPosBuffer { // influencee "rigidbody"
     vec4 bodyPos[];
};
layout(std430, binding = 3) readonly buffer bodyTriBuffer {
    vec4 bodyTriangles[];
};
layout(std430, binding = 4) buffer collisonBuffer { // vec4s of normal dir and distance 
    vec4 pClothCollisionConstraints[];
};
layout(std430, binding = 5) buffer debugBuffer { // vec4s of debug data
    vec4 debug[];
};

layout(local_size_x = WORK_GROUP_SIZE_X, local_size_y = WORK_GROUP_SIZE_Y, local_size_z = 1) in;

layout(location = 0) uniform int numTriangles;
layout(location = 1) uniform int numPositions;
layout(location = 2) uniform float staticConstraintBounce;


float calculateUV(float f1,float f2,float f3, float f4, float signedDistance)
{
    return (f1 * f2 - f3 * f4) * signedDistance;
}

float getDot(vec3 v1, vec3 P, vec3 v2)
{
    return -dot(v1 - P, v2 - v1) / pow(length(v2 - v1), 2.0f);
}

vec3 nearestPointOnTriangle(vec3 P, vec3 A, vec3 B, vec3 C)
{
    vec3 v0 = C - A;
    vec3 v1 = B - A;
    vec3 norm = cross(normalize(v1), normalize(v0));

    // case 1: it's in the triangle
    // project into triangle plane
    // (project an arbitrary vector from P to triangle onto the normal)
    vec3 w = A - P;
    float signedDistance_invDenom = dot(norm, w);
    vec3 projP = P + signedDistance_invDenom * norm;

    // compute u v coordinates
    // http://www.blackpawn.com/texts/pointinpoly/
    //u = ((v1.v1)(v2.v0) - (v1.v0)(v2.v1)) / ((v0.v0)(v1.v1) - (v0.v1)(v1.v0))
    //v = ((v0.v0)(v2.v1) - (v0.v1)(v2.v0)) / ((v0.v0)(v1.v1) - (v0.v1)(v1.v0))
    norm = projP - A;
    float dotAB = dot(v0, v0);
    float dotBC = dot(v0, v1);
    float dotCA = dot(v0, norm);
    float minDistance = dot(v1, v1);
    float candidate = dot(v1, norm);

    signedDistance_invDenom = 1 / (dotAB * minDistance - dotBC * dotBC);

    float u_t = calculateUV(minDistance,dotCA,dotBC,candidate,signedDistance_invDenom);
    float v = calculateUV(dotAB,candidate, dotBC, dotCA, signedDistance_invDenom);

    // if the u v is in bounds, we can return the projected point
    if (u_t >= 0.0 && v >= 0.0 && (u_t + v) <= 1.0) {
      return projP;
      }

      // case 2: it's on an edge
      // http://mathworld.wolfram.com/Point-LineDistance3-Dimensional.html
      // t = - (x1 - x0) dot (x2 - x1) / len(x2 - x1) ^ 2
      // x1 is the "line origin," x2 is the "towards" point, and x0 is the outlier
      // check A->B edge
      dotAB =  getDot(A,P,B);

      // check B->C edge
      dotBC = getDot(B,P,C);

      // check C->A edge
      dotCA = getDot(C,P,A);

    // handle case 3: point is closest to a vertex
    dotAB = clamp(dotAB, 0.0f, 1.0f);
    dotBC = clamp(dotBC, 0.0f, 1.0f);
    dotCA = clamp(dotCA, 0.0f, 1.0f);

      // assess each edge's distance and parametrics
    minDistance = length(cross(P - A, P - B)) / length(B - A);
    if(candidate < minDistance)
    {  
    minDistance = candidate;
       v0 = A;
       v1 = B;
       u_t = dotAB;
    }
    
    candidate = length(cross(P - C, P - A)) / length(C - A);
    if (candidate < minDistance) {
            minDistance = candidate;
            v0 = C;
            v1 = A;
            u_t = dotCA;
    }

    candidate = length(cross(P - B, P - C)) / length(B - C);
    if (candidate < minDistance) {
      minDistance = candidate;
      v0 = B;
      v1 = C;
      u_t = dotBC;
      }

    return (u_t * (v1 - v0) + v0);
}

void generateStaticConstraint(vec3 pos) {
    uint idx = gl_GlobalInvocationID.x;

    // static constraint: generate a "point of entry" approximating the closest
    // point on the mesh to the pos from the last timestep (pos).
    // Move the position in the last timestep based on this "point of entry" and
    // use the normal at this point to generate a constraint that will get
    // the point in this timestep out.

    vec3 candidatePoint;
    float candidateDistance;

    vec3 nearestPoint;
    vec3 nearestNormal;
    float nearestDistance;

    vec3 triangle;
    vec3 v0 ;
    vec3 v1 ;
    vec3 v2 ;


    for(int i = 0; i < numTriangles; i++) {

        triangle = bodyTriangles[i].xyz;
        v0 = bodyPos[int(triangle.x)].xyz;
        v1 = bodyPos[int(triangle.y)].xyz;
        v2 = bodyPos[int(triangle.z)].xyz;
        candidatePoint = nearestPointOnTriangle(pos, v0, v1, v2);
        candidateDistance = length(candidatePoint - pos);

        if (i == 0 || candidateDistance < nearestDistance) {
            nearestDistance = candidateDistance;
            nearestPoint = candidatePoint;
            nearestNormal = normalize(cross(v1 - v0, v2 - v0));
         }

    }

    // move the position in the last timestep over to nearestPoint
    preCloth[idx].xyz = nearestPoint + nearestNormal * staticConstraintBounce;
    pClothCollisionConstraints[idx] = vec4(nearestNormal, 1.0);
    return;
}

float mollerTrumboreIntersectTriangle(vec3 orig, vec3 dir, vec3 v0, vec3 v1, vec3 v2)
{
    
    // adapted from Moller-Trumbore intersection algorithm pseudocode on wikipedia
 // Edge1, Edge2
    float u, v;
    float t;
    
    // vectors for edges sharing V1
   vec3 e1 = v1 - v0;
   vec3 e2 = v2 - v0;

    // begin calculating determinant - also used to calculate u param
   vec3 ray_cross_e2 = cross(dir, e2);

    // if determinant is near zero, ray lies in plane of triangle
   float det = dot(e1, ray_cross_e2);
    // NOT culling
    if (det > -EPSILON && det < EPSILON) return -1.0;
    float inv_det = 1.0 / det;

    // calculate distance from v0 to ray origin
    vec3 S = orig - v0;

    // calculate u parameter and test bound
    u = dot(S, ray_cross_e2) * inv_det;
    // the intersection lies outside of the triangle
    if (u < 0.0 || u > 1.0) return -1.0;

    // prepare to test v parameter
    vec3 s_cross_e1 = cross(S, e1);

    // calculate v param and test bound
    v = dot(dir, s_cross_e1) * inv_det;

    // the intersection is outside the triangle?
    if (v < 0.0 || (u + v) > 1.0) return -1.0;

    t = dot(e2, s_cross_e1) * inv_det;

    if (t > 0.0) {
        return t;
    }

    return -1.0;
}

void main() {
    uint idx = gl_GlobalInvocationID.x;
    if (idx >= numPositions) return;

    // check if there's already a valid constraint. if so, do nothing
    if (pClothCollisionConstraints[idx].w >= 0.0) return; 

    // also, if this is infinite weighted, do nothing
    // inverse mass is the w in the position
    if (preCloth[idx].w < EPSILON) return;

    vec3 pos = preCloth[idx].xyz; // prev timestep
    vec3 lookAt = curCloth[idx].xyz; // next timestep
    float dirScale = length(lookAt - pos);
    vec3 dir = normalize(lookAt - pos);

    vec4 collisionConstraint = vec4(-1.0); // a bogus collisionConstraint

    // if there's an odd number of collisions, we're inside the mesh already
    int numCollisions = 0; // which means we need a static constraint (addtl handling here)

    debug[idx] = vec4(-1.0);
    vec3 debugPos;

    // check against every triangle in the mesh.
    for (int i = 0; i < numTriangles; i++) {

        vec3 triangle = bodyTriangles[i].xyz;
        vec3 v0 = bodyPos[int(triangle.x)].xyz;
        vec3 v1 = bodyPos[int(triangle.y)].xyz;
        vec3 v2 = bodyPos[int(triangle.z)].xyz;
        vec3 norm = normalize(cross(v1 - v0, v2 - v0));

        // b/c intersectTriangle gets us a distance with a normalized dir vector
        // intersectTriangle = realLength * dirScale
        // intersectTriangle / dirScale = realLength
        float collisionT = mollerTrumboreIntersectTriangle(pos, dir, v0, v1, v2);

        vec3 intersectedPoint = pos + collisionT * dir;
        
        // collision out of bounds
        if (collisionT > -EPSILON) {
            numCollisions++;
            debugPos = pos + (collisionT / dirScale) * (lookAt - pos);
        }
        collisionT /= dirScale;
        if (collisionT > 1.0 || collisionT < 0.0) {
            continue;
        }
        //use the nearest collision with distance less than 1
        if (collisionConstraint.w < 0.0 || collisionT < collisionConstraint.w) {
           collisionConstraint.xyz =  norm;    
           collisionConstraint.w = collisionT;
        }
    }
    debug[idx].xyz = debugPos;
    debug[idx].w = numCollisions;

    // if the number of collisions is odd
    // and no triangle was crossed in the timestep, <- ? seems logical but leads to odd results
    // generate a static constraint instead.
    if (numCollisions % 2 != 0) {//} && collisionConstraint.w < 0.0) {
        //pCloth2[idx].xyz = debugPos;//vec3(0.0, 0.0, -numCollisions); // debug
        generateStaticConstraint(pos);
        return;
    }

    pClothCollisionConstraints[idx] = collisionConstraint;
}
